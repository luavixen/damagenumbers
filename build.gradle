plugins {
    id 'architectury-plugin' version '3.4.155'
    id 'dev.architectury.loom' version '1.6.397' apply false
}

group = project.mod_group
version = project.mod_version

architectury {
    minecraft = project.minecraft_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_version}:v2"

        modImplementation "dev.isxander.yacl:yet-another-config-lib-common:${project.yacl_version}"
    }

    configurations.modImplementation {
        exclude group: 'com.twelvemonkeys.common'
        exclude group: 'com.twelvemonkeys.imageio'
    }

    jar {
        manifest {
            attributes([
                'Specification-Title'     : rootProject.mod_name,
                'Specification-Vendor'    : 'luavixen',
                'Specification-Version'   : '1',
                'Implementation-Title'    : 'Damage Numbers',
                'Implementation-Version'  : rootProject.mod_version,
                'Implementation-Vendor'   : 'luavixen',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'architectury-plugin'

    base {
        archivesName = rootProject.mod_name
    }

    version = "${rootProject.mod_version}-${project.name}"
    group = rootProject.mod_group

    repositories {
        maven {
            name = 'NeoForged Maven'
            url = 'https://maven.neoforged.net/releases/'
        }
        maven {
            name 'Xander Maven'
            url 'https://maven.isxander.dev/releases'
        }
        maven {
            name 'TerraformersMC Maven'
            url 'https://maven.terraformersmc.com/releases'
        }
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    compileJava {
        options.release = 17
        options.encoding = 'UTF-8'
    }

    processResources {
        filesMatching(['fabric.mod.json', 'META-INF/mods.toml', 'META-INF/*.mods.toml']) {
            expand 'version': project.version
        }
    }
}
