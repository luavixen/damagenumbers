plugins {
    id 'dev.architectury.loom' version '1.5.388'
}

group = project.mod_group
archivesBaseName = project.mod_name

version = project.mod_version

loom {
    forge {
        mixinConfig 'damagenumbers.mixins.json'
    }
    runs {
        data {
            data()
            programArgs '--existing', file('src/main/resources').absolutePath
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name 'Xander Maven'
        url 'https://maven.isxander.dev/releases'
    }
    maven {
        name 'TerraformersMC Maven'
        url 'https://maven.terraformersmc.com/releases'
    }
}


dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"
    modImplementation "dev.isxander.yacl:yet-another-config-lib-forge:${project.yacl_forge_version}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand 'version': version
    }
}

jar {
    manifest {
        attributes([
            'Specification-Title'     : project.mod_name,
            'Specification-Vendor'    : 'luavixen',
            'Specification-Version'   : '1',
            'Implementation-Title'    : 'Damage Numbers',
            'Implementation-Version'  : project.mod_version,
            'Implementation-Vendor'   : 'luavixen',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
